plugins {
    id 'org.gradle.samples.cpp-header-library' version '1.0'
    id 'xcode'
    id 'visual-studio'
    id 'maven-publish'
    id 'cpp-unit-test'
}

group = 'org.gradle.cpp-samples'
version = '1.5'

repositories {
    maven {
        url = 'https://repo.gradle.org/gradle/libs-snapshots-local/'
    }
}

// Because the Gradle publishing format for C++ is not yet stable, encode the Gradle metadata version in the version as well
// This will not be necessarily in later releases and Gradle will take care of this instead
unitTest {
    dependencies {
        implementation "org.gradle.cpp-samples:googletest:1.9.0-gr2-SNAPSHOT"
    }
    binaries.configureEach {
        if (toolChain instanceof Gcc && targetPlatform.operatingSystem.linux) {
            linkTask.get().linkerArgs.add("-lpthread")
        }
    }
}

publishing {
    repositories {
        maven {
            url = '../repo'
        }
    }
}
