plugins {
    id 'swift-application'
    id 'xcode'
    id 'xctest'
}

group = 'org.gradle.swift-samples'

application {
    dependencies {
        implementation 'org.gradle.swift-samples:utilities:1.0'
    }
}

def linuxMainWorkaround(Project project, File testDir) {
    project.xctest {
        source.from project.fileTree(dir: testDir, include: '**/*.swift', exclude: 'LinuxMain.swift')
    }

    if (System.properties['os.name'].equals("Linux")) {
        // On linux, rename LinuxMain.swift -> main.swift
        def renameLinuxMain = project.tasks.register("renameLinuxMain", Sync) {
            from testDir
            into temporaryDir
            include 'LinuxMain.swift'
            rename { 'main.swift' }
        }
        project.xctest {
            source.from renameLinuxMain
        }
    }
}

linuxMainWorkaround(project, file('src/test/swift'))
